// BSLLS-выкл.
#Область СлужебныйПрограммныйИнтерфейс

// @unit-test
Процедура Тест_НайтиПоСекретномуТокенуПроверкаРезультата(Фреймворк) Экспорт
	
	Фреймворк.ПроверитьНеЗаполненность(Справочники.ОбработчикиСобытий.НайтиПоСекретномуТокену(1));
	Фреймворк.ПроверитьНеЗаполненность(Справочники.ОбработчикиСобытий.НайтиПоСекретномуТокену(""));
	Фреймворк.ПроверитьНеЗаполненность(Справочники.ОбработчикиСобытий.НайтиПоСекретномуТокену(Неопределено));
	Фреймворк.ПроверитьНеЗаполненность(Справочники.ОбработчикиСобытий.НайтиПоСекретномуТокену("блаблаблаблака"));

	НачатьТранзакцию();
	
	Попытка
		СекретныйТокен = "ЮнитТест";
		
		НовыйЭлемент = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйТокен);
		Код1 = НовыйЭлемент.Ссылка.Код;
		
		НовыйЭлемент = ДобавитьОбработчикСобытий("ЮнитТест2", СекретныйТокен);
		Код2 = НовыйЭлемент.Ссылка.Код;
		
		Фреймворк.Что(Код2).Больше(Код1);
		Фреймворк.Что(Справочники.ОбработчикиСобытий.НайтиПоСекретномуТокену(СекретныйТокен).Код).Равно(Код2);
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьОбработчикСобытий(Знач Наименование = "", Знач СекретныйТокен = "") Экспорт
	
		НовыйЭлемент = Справочники.ОбработчикиСобытий.СоздатьЭлемент();
		НовыйЭлемент.Наименование = Наименование;
		НовыйЭлемент.СекретныйТокен = СекретныйТокен;
		НовыйЭлемент.Записать();
		
		Возврат НовыйЭлемент;
	
КонецФункции

#КонецОбласти