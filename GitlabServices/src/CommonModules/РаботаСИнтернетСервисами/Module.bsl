////////////////////////////////////////////////////////////////////////////////
//
// Работа с интернет сервисами.
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Формирование краткого описания сервиса по метаданным конфигурации в виде коллекции (см. API REST сервиса).
// 
// Параметры:
// 	ИмяСервиса - Строка - имя сервиса в конфигурации;
// Возвращаемое значение:
// 	Структура - коллекция с описанием сервиса:
//	* name - Строка - имя сервиса;
//	* desc - Строка - комментарий к сервису;
// 	* enabled - Булево - Истина - функциональность включена, иначе - Ложь;
// 	* templates - Массив из Структура - шаблоны URL:
//		** name - Строка - имя шаблона URL;
//		** desc - Строка - комментарий к шаблону URL;
//		** template - Строка - шаблон;
//		** methods - Массив из Структура - методы:
//			*** name - Строка - имя метода сервиса;
//			*** desc - Строка - комментарий к методу сервиса;
//			*** method - Строка - HTTP-метод сервиса;
//			
// @UnitTest
//
Функция ПолучитьОписаниеСервиса( Знач ИмяСервиса ) Экспорт
	
	Перем МетаданныеСервис;
	
	МетаданныеСервис = Метаданные.HTTPСервисы.Найти( ИмяСервиса );
	
	Если (МетаданныеСервис = Неопределено) Тогда
		
		Возврат Новый Структура();
		
	КонецЕсли;
	
	Возврат ПолучитьКоллекциюСОписаниемСервиса( ИмяСервиса, МетаданныеСервис );
	
КонецФункции

// Получение описания веб-сервиса по URL c десериализацией.
// 
// Параметры:
// 	URL - Строка - URL веб-сервиса; 
// Возвращаемое значение:
// 	- Неопределено - если сервис не найден, иначе возвращает коллекцию с ответом и телом ответа в различных форматах;
// 	- ФиксированнаяСтруктура - описание:
//	* Ответ - Структура - (См. КоннекторHTTP.Get)
//	* Соответствие - Соответствие - (См. КоннекторHTTP.КакJson)
// 	* json - Строка - (См. КоннекторHTTP.КакТекст)
//			
// @UnitTest
//
Функция ПолучитьОписаниеСервисаURL( Знач URL ) Экспорт
	
	Перем Ответ;
	Перем Результат;

	Результат = Неопределено;
	
	Если ( ТипЗнч(URL) <> Тип("Строка") ИЛИ ПустаяСтрока(URL) ) Тогда
		
		Возврат Результат;
		 					
	КонецЕсли;
	
	Ответ = КоннекторHTTP.Get( URL );
			
	Если ( ЗначениеЗаполнено(Ответ) И ЗначениеЗаполнено(Ответ.Тело) ) Тогда
		
		Результат = Новый Структура();
		Результат.Вставить( "Ответ", Ответ );
		Результат.Вставить( "Соответствие", КоннекторHTTP.КакJson(Ответ) );
		Результат.Вставить( "json", КоннекторHTTP.КакТекст(Ответ, КодировкаТекста.UTF8) );
		
		Результат = Новый ФиксированнаяСтруктура( Результат );
		
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции





// Устарела.
// Устанавливает тело HTTP-ответа в виде строки в формате JSON. Если передана строка, то в тело ответа записывается
// строка без ее предварительной сериализации и проверки структуры содержимого на соответствие формату JSON.
// Если передана структура, то производится предварительная сериализация структуры в строку в формате JSON.
// 
// Параметры:
// 	HTTPСервисОтвет - HTTPСервисОтвет - HTTP-ответ.
// 	Данные - Строка, Структура - строка в формате JSON или произвольная структура,
//		которая может быть сериализована в формат JSON.
//			
// @tested 
Процедура УстановитьТелоОтветаИзJSON(HTTPСервисОтвет, Знач Данные) Экспорт
	
	Перем СтрокаJSON;
	
	СтрокаJSON = ?( (ТипЗнч(Данные) = Тип("Структура")), СтруктураВJSON(Данные), Данные );			
	HTTPСервисОтвет.Заголовки.Вставить("Content-Type", "application/json");
	HTTPСервисОтвет.УстановитьТелоИзСтроки(СтрокаJSON, КодировкаТекста.UTF8);

КонецПроцедуры

// TODO пересмотреть, возможно избыточна
// Добавляет сообщение в тело ответа в формате JSON по событиям журнала регистрации. Если событие не указано, то
// по умолчанию выбирается сообщение уровня "Информация".
// 
// Параметры:
// 	HTTPСервисОтвет - HTTPСервисОтвет - HTTP-ответ.
// 	Текст - Строка - текст сообщения.
//	Событие - ПеречислениеСсылка.УровеньЖурналаРегистрации - уровень важности события журнала регистрации.
//			
// @tested
Процедура СобытиеЖурналаРегистрацииВТелоОтвета(HTTPСервисОтвет, Знач Текст, Знач Событие = Неопределено) Экспорт
	
	Перем ПеречислениеУровеньЖурналаРегистрации;
	Перем Сообщение;
	
	ПеречислениеУровеньЖурналаРегистрации = ( ТипЗнч(Событие) = Тип("ПеречислениеСсылка.УровеньЖурналаРегистрации") );
	
	Если ( (Событие = Неопределено) ИЛИ (НЕ ПеречислениеУровеньЖурналаРегистрации) ) Тогда
		Событие = Перечисления.УровеньЖурналаРегистрации.Информация;				
	КонецЕсли;
	
	Сообщение = Новый Структура;
	Сообщение.Вставить(Строка(Событие), Новый Структура("Сообщение", Текст));
	УстановитьТелоОтветаИзJSON(HTTPСервисОтвет, Сообщение);

КонецПроцедуры

// TODO пересмотреть, возможно избыточна
// Получает тело HTTP-ответа в формате JSON и преобразует его в коллекцию. Если ДобавлятьИсточник=Истина,
// то дополнительно в коллекцию по ключу "json" добавляется элемент с текстом тела HTTP-ответа в формате JSON. 
// 
// Параметры:
// 	HTTPОтвет - HTTPОтвет - HTTP-ответ;
//	ЭтоСоответствие - Булево - если Истина, то JSON будет преобразован в Соответствие, если Ложь, то в Структуру;
// 	ДобавлятьИсточник - Булево - если Истина, то в коллекцию по ключу "json" добавляется элемент
// 		с текстом в формате JSON;
// 	Коллекция - Неопределено, Соответствие, Структура - (исходящий параметр),
//		тело запроса преобразованное из текста в формате JSON в Соответствие или Структуру;
//			
// @tested
Процедура ТелоHTTPОтветаВКоллекциюКакJSON(Знач HTTPОтвет, Знач ЭтоСоответствие = Ложь, Знач ДобавлятьИсточник = Ложь,
				Коллекция = Неопределено) Экспорт
	
	Перем Поток;
	
	Поток = HTTPОтвет.ПолучитьТелоКакПоток();

	Попытка
		
		ОбщегоНазначения.ПотокВКоллекциюКакJSON( Поток, ЭтоСоответствие, ДобавлятьИсточник, Коллекция );
		
	Исключение

		Поток.Закрыть();
		ВызватьИсключение;
	
	КонецПопытки;
	
	Поток.Закрыть();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьКоллекциюСОписаниемСервиса( Знач ИмяСервиса, Знач МетаданныеСервиса )

	Перем Результат;
	
	Результат = Новый Структура();
	Результат.Вставить( "name", ИмяСервиса );
	Результат.Вставить( "desc", МетаданныеСервиса.Комментарий );
	Результат.Вставить( "enabled", ПолучитьФункциональнуюОпцию( "ЗагружатьФайлыИзВнешнегоХранилища" ) );
	
	Результат.Вставить( "templates", ПолучитьОписаниеШаблоновСервиса( МетаданныеСервиса.ШаблоныURL ) );
	
	Возврат Результат;

КонецФункции

Функция ПолучитьОписаниеМетодовСервиса( Знач Методы )
	
	Перем НовыйОписаниеМетода;
	Перем Результат;
	
	Результат = Новый Массив();
	
	Для каждого Метод Из Методы Цикл
		
		НовыйОписаниеМетода = Новый Структура();
		НовыйОписаниеМетода.Вставить( "name", Метод.Имя );
		НовыйОписаниеМетода.Вставить( "desc", Метод.Комментарий );
		НовыйОписаниеМетода.Вставить( "method", Строка(Метод.HTTPМетод) );
		
		Результат.Добавить( НовыйОписаниеМетода );
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Функция ПолучитьОписаниеШаблоновСервиса( Знач Шаблоны )
	
	Перем НовыйОписаниеШаблона;
	Перем Результат;

	Результат = Новый Массив();
	
	Для каждого Шаблон Из Шаблоны Цикл
		
		НовыйОписаниеШаблона = Новый Структура();
		НовыйОписаниеШаблона.Вставить( "name", Шаблон.Имя );
		НовыйОписаниеШаблона.Вставить( "desc", Шаблон.Комментарий );
		НовыйОписаниеШаблона.Вставить( "template", Шаблон.Шаблон );

		НовыйОписаниеШаблона.Вставить( "methods", ПолучитьОписаниеМетодовСервиса( Шаблон.Методы ) );
		Результат.Добавить( НовыйОписаниеШаблона );
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции


Функция СтруктураВJSON(Знач Данные)
	
	Перем ЗаписьJSON;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

#КонецОбласти