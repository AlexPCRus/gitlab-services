Функция Version(Запрос)
	
	Перем Ответ;
	Перем ОписаниеСервиса;
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ОписаниеСервиса = РаботаСИнтернетСервисами.ОписаниеСервиса("gitlab");
	
	Структура = Новый Структура;
	Структура.Вставить("version", "1.0.1.11");
	Структура.Вставить("services", ОписаниеСервиса);
	
	РаботаСИнтернетСервисами.УстановитьТелоОтветаИзСтруктурыВФорматеJSON(Ответ, Структура);
	
	Возврат Ответ;
	
КонецФункции

Функция WebhookUpdates(Запрос)
	
	Перем Ответ;
	Перем ДанныеТелаЗапроса;
	
	Ответ = Новый HTTPСервисОтвет(400);
	
	ИдентификаторWebhook = Неопределено;
	СервисыGitLab.ЗаполнитьИдентификаторWebhook(Запрос, ИдентификаторWebhook);
	
	Если ИдентификаторWebhook = Справочники.Webhooks.ПустаяСсылка() Тогда
		
		Ответ = Новый HTTPСервисОтвет(403);
		СервисыGitLab.ЗалогироватьОшибку("GitLab",
										 "ОбработкаЗапроса",
										 ИдентификаторWebhook,
										 НСтр("ru = 'Секретный токен не найден.'"),
										 Ответ);
										 
		Возврат Ответ;
		
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ЗагружатьФайлыИзВнешнегоХранилища") Тогда
		
		Ответ = Новый HTTPСервисОтвет(423);
		СервисыGitLab.ЗалогироватьПредупреждение("GitLab",
												 "ОбработкаЗапроса",
												 ИдентификаторWebhook,
												 НСтр("ru = 'Функционал загрузки из внешнего хранилища отключен.'"),
												 Ответ);
		
		Возврат Ответ;
		
	КонецЕсли;
	
	Ответ = Неопределено;
	СервисыGitLab.ПроверитьЗапросUpdates(ИдентификаторWebhook, Запрос, Ответ);
	
	Если Ответ.КодСостояния = 200 Тогда
		
		ДанныеТелаЗапроса = Неопределено;
		СервисыGitLab.ПолучитьДанныеТелаЗапроса(ИдентификаторWebhook, Запрос, Ответ, ДанныеТелаЗапроса);
		
	КонецЕсли;
	
	Если Ответ.КодСостояния = 200 Тогда
		
		СервисыGitLab.ПроверитьТелоЗапроса(ИдентификаторWebhook, ДанныеТелаЗапроса, Ответ);
		
	КонецЕсли;
	
	Если Ответ.КодСостояния = 200 Тогда
		
		СервисыGitLab.ЗапуститьОбработкуДанныхВФоне(ИдентификаторWebhook, ДанныеТелаЗапроса);
		
	КонецЕсли;
	
	Если Ответ.КодСостояния = 200 Тогда
		
		СервисыGitLab.ЗалогироватьИнформацию("GitLab",
											 "ОбработкаЗапроса.Окончание",
											 ИдентификаторWebhook,
											 НСтр("ru = 'Запрос обработан.'"),
											 Ответ);
											 
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции