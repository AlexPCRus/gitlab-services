////////////////////////////////////////////////////////////////////////////////
// Работа с HTTP-сервисами (hs).
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Формирование краткого описания сервиса по метаданным конфигурации в виде коллекции.
// 
// Параметры:
// 	ИмяСервиса - Строка - имя сервиса в конфигурации.
// Возвращаемое значение:
// 	Структура - коллекция с описанием сервиса:
//	* name - Строка - имя сервиса.
//	* desc - Строка - комментарий к сервису.
// 	* enabled - Булево - Истина - функциональность включена, иначе - Ложь.
// 	* groups - Массив из Структура - описание:
//		** name - Строка - имя шаблона URL.
//		** desc - Строка - комментарий к шаблону URL.
//		** template - Строка - шаблон.
//		** methods - Массив из Структура - описание:
//			*** name - Строка - имя метода сервиса.
//			*** desc - Строка - комментарий к методу сервиса.
//			*** method - Строка - HTTP-метод сервиса.
//			
// @tested
Функция ОписаниеСервиса(Знач ИмяСервиса) Экспорт
	
	Перем Описание;
	Перем МетаданныеСервиса;
	
	Описание = Новый Структура;
	
	МетаданныеСервиса = Метаданные.HTTPСервисы.Найти(ИмяСервиса);
	Если (МетаданныеСервиса = Неопределено) Тогда
		Возврат Описание;
	КонецЕсли;
	
	ПодготовитьОписаниеСервиса(ИмяСервиса, МетаданныеСервиса, Описание);
	
	Возврат Описание;
	
КонецФункции

// Получение краткого описания веб-сервиса по URL.
// 
// Параметры:
// 	URL - Строка - URL веб-сервиса. 
// Возвращаемое значение:
// 	- Неопределено - если сервис не найден, иначе возвращает коллекцию с ответом и телом ответа в различных форматах;
// 	- ФиксированнаяСтруктура - описание:
//	* Ответ - Структура - (См. КоннекторHTTP.Get)
//	* Соответствие - Соответствие - (См. КоннекторHTTP.КакJson)
// 	* JSON - Строка - (См. КоннекторHTTP.КакТекст)
//			
// @tested
Функция ОписаниеСервисаПоАдресу(Знач URL) Экспорт
	
	Перем Результат;
	Перем Ответ;

	Результат = Неопределено;
	Если ( ТипЗнч(URL) <> Тип("Строка") ИЛИ ПустаяСтрока(URL) ) Тогда
		Возврат Результат; 					
	КонецЕсли;
	
	Ответ = КоннекторHTTP.Get(URL);		
	Если ( ЗначениеЗаполнено(Ответ) И ЗначениеЗаполнено(Ответ.Тело) ) Тогда
		Результат = Новый Структура;
		Результат.Вставить("Ответ", Ответ);
		Результат.Вставить("Соответствие", КоннекторHTTP.КакJson(Ответ));
		Результат.Вставить("JSON", КоннекторHTTP.КакТекст(Ответ, КодировкаТекста.UTF8));
		Результат = Новый ФиксированнаяСтруктура(Результат);
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// TODO вынести в общий модуль или найти аналог.
// Проверяет наличие элемента в соответствии по ключу и сравнивает значение этого элемента с переданным
// в функцию значением.
// 
// Параметры:
// 	Соответствие - ФиксированноеСоответствие - проверяемая коллекция.
// 	Ключ - Строка - имя искомого ключа.
// 	ПроверяемоеЗначение - Строка - проверяемое значение.
// Возвращаемое значение:
// 	- Булево - Истина, если элемент существует и его значение равно проверяемому значению, иначе - Ложь.
//			
// @tested
Функция ЗначениеВКоллекцииСуществует(Знач Соответствие, Знач Ключ, Знач ПроверяемоеЗначение = "") Экспорт
	
	Перем Значение;
	
	Значение = Соответствие.Получить(Ключ);

	Возврат НЕ ( (Значение = Неопределено) ИЛИ (Значение <> ПроверяемоеЗначение) );
	
КонецФункции

// Устанавливает тело HTTP-ответа в виде строки в формате JSON. Если передана строка, то в тело ответа записывается
// строка без ее предварительной сериализации и проверки структуры содержимого на соответствие формату JSON.
// Если передана структура, то производится предварительная сериализация структуры в строку в формате JSON.
// 
// Параметры:
// 	HTTPСервисОтвет - HTTPСервисОтвет - HTTP-ответ.
// 	Данные - Строка, Структура - строка в формате JSON или произвольная структура,
//		которая может быть сериализована в формат JSON.
//			
// @tested 
Процедура УстановитьТелоОтветаИзJSON(HTTPСервисОтвет, Знач Данные) Экспорт
	
	Перем СтрокаJSON;
	
	СтрокаJSON = ?( (ТипЗнч(Данные) = Тип("Структура")), СтруктураВJSON(Данные), Данные );			
	HTTPСервисОтвет.Заголовки.Вставить("Content-Type", "application/json");
	HTTPСервисОтвет.УстановитьТелоИзСтроки(СтрокаJSON, КодировкаТекста.UTF8);

КонецПроцедуры

// Добавляет сообщение в тело ответа в формате JSON по событиям журнала регистрации. Если событие не указано, то
// по умолчанию выбирается сообщение уровня "Информация".
// 
// Параметры:
// 	HTTPСервисОтвет - HTTPСервисОтвет - HTTP-ответ.
// 	Текст - Строка - текст сообщения.
//	Событие - ПеречислениеСсылка.УровеньЖурналаРегистрации - уровень важности события журнала регистрации.
//			
// @tested
Процедура СобытиеЖурналаРегистрацииВТелоОтвета(HTTPСервисОтвет, Знач Текст, Знач Событие = Неопределено) Экспорт
	
	Перем ПеречислениеУровеньЖурналаРегистрации;
	Перем Сообщение;
	
	ПеречислениеУровеньЖурналаРегистрации = ( ТипЗнч(Событие) = Тип("ПеречислениеСсылка.УровеньЖурналаРегистрации") );
	
	Если ( (Событие = Неопределено) ИЛИ (НЕ ПеречислениеУровеньЖурналаРегистрации) ) Тогда
		Событие = Перечисления.УровеньЖурналаРегистрации.Информация;				
	КонецЕсли;
	
	Сообщение = Новый Структура;
	Сообщение.Вставить(Строка(Событие), Новый Структура("Сообщение", Текст));
	УстановитьТелоОтветаИзJSON(HTTPСервисОтвет, Сообщение);

КонецПроцедуры

// Получает тело HTTP-ответа в формате json и преобразует его в коллекцию.
// 
// Параметры:
// 	HTTPОтвет - HTTPОтвет - HTTP-ответ;
//	ЭтоСоответствие - Булево - если Истина, то json будет преобразован в Соответствие, если Ложь - в Структуру;
// 	ДанныеТела - Неопределено, Соответствие, Структура - исходящий параметр,
//		тело запроса преобразованное из формата json в Соответствие или Структура. 
Процедура ПреобразоватьТелоHTTPОтветаИзФорматаJSON(Знач HTTPОтвет, Знач ЭтоСоответствие = Ложь, ДанныеТела = Неопределено) Экспорт
	
	Перем Поток;
	
	Поток = HTTPОтвет.ПолучитьТелоКакПоток();
	ПреобразоватьПотокИзФорматаJSONВКоллекцию(Поток, ЭтоСоответствие, ДанныеТела);
		
КонецПроцедуры

// Получает поток данных и преобразует его в коллекцию через чтение потока как JSON.
// 
// Параметры:
// 	Поток - Поток - поток с данными;
// 	ЭтоСоответствие - Булево - если Истина, то JSON будет преобразован в Соответствие, если Ложь, то в Структуру;
// 	Данные - Неопределено, Соответствие, Структура - (исходящий параметр), преобразованные данные
//		из формата JSON в Соответствие или Структуру. 
Процедура ПреобразоватьПотокИзФорматаJSONВКоллекцию(Знач Поток,
													Знач ЭтоСоответствие,
														 Данные = Неопределено) Экспорт
	
	Перем ЧтениеJSON;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	
	Попытка
		
		ЧтениеJSON.ОткрытьПоток(Поток, КодировкаТекста.UTF8);
		Данные = ПрочитатьJSON(ЧтениеJSON, ЭтоСоответствие);
		ЧтениеJSON.Закрыть();
		
		Попытка
			ДополнитьДанныеИсходнымТекстомJSON(Поток, "json", Данные);
			Поток.Закрыть();
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
	Исключение
		ЧтениеJSON.Закрыть();
		Поток.Закрыть();
		
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Дополняет коллекцию данных элементом в виде преобразованного в текст потока, переданного
// в процедуру в качестве параметра.
// 
// Параметры:
// 	Поток - Поток - поток с данными;
// 	Ключ - Строка - ключ добавляемого в коллекцию элемента;
// 	Данные - Структура, Соответствие - коллекция, в которую добавляется преобразованный в текст поток;
Процедура ДополнитьДанныеИсходнымТекстомJSON(Знач Поток, Знач Ключ, Данные) Экспорт
	
	Перем ИсходныйТекст;

	Попытка
		
		Поток.Перейти(0, ПозицияВПотоке.Начало);
		ЧтениеТекста = Новый ЧтениеТекста(Поток, КодировкаТекста.UTF8);
		ИсходныйТекст = ЧтениеТекста.Прочитать();
		Данные.Вставить(Ключ, ИсходныйТекст);
		ЧтениеТекста.Закрыть();
		
	Исключение
		ЧтениеТекста.Закрыть();
		Поток.Закрыть();
		
		ВызватьИсключение;

	КонецПопытки;
	

КонецПроцедуры

// Получает тело HTTP-ответа в формате json и преобразует его в Соответствие.
// 
// Параметры:
// 	HTTPОтвет - HTTPОтвет - HTTP-ответ;
// 	ДанныеТела - Неопределено, Соответствие - исходящий параметр,
//		тело запроса преобразованное из формата json в Соответствие или Структура. 
Процедура ПреобразоватьТелоHTTPОтветаИзФорматаJSONВСоответствие(Знач HTTPОтвет, ДанныеТела = Неопределено) Экспорт
	
	ПреобразоватьТелоHTTPОтветаИзФорматаJSON(HTTPОтвет, Истина, ДанныеТела);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьОписаниеСервиса(Знач ИмяСервиса, Знач МетаданныеСервиса, Описание)

	Перем ШаблоныURL;	
	Перем ОписаниеШаблоновURL;
	
	Описание.Вставить("name", ИмяСервиса);
	Описание.Вставить("desc", МетаданныеСервиса.Комментарий);
	Описание.Вставить("enabled", ПолучитьФункциональнуюОпцию("ЗагружатьФайлыИзВнешнегоХранилища"));
	
	ШаблоныURL = МетаданныеСервиса.ШаблоныURL;
	ОписаниеШаблоновURL = ОписаниеШаблоновСервиса(ШаблоныURL);
	
	Описание.Вставить("groups", ОписаниеШаблоновURL);

КонецПроцедуры

Функция ОписаниеШаблоновСервиса(Знач ШаблоныURL)
	
	Перем ШаблонURL;
	Перем ОписаниеШаблонаURL;
	Перем ОписаниеШаблоновURL;
	
	Перем Методы;
	Перем ОписаниеМетодов;

	ОписаниеШаблоновURL = Новый Массив;
	Для каждого ШаблонURL Из ШаблоныURL Цикл
		
		ОписаниеШаблонаURL = Новый Структура;
		ОписаниеШаблонаURL.Вставить("name", ШаблонURL.Имя);
		ОписаниеШаблонаURL.Вставить("desc", ШаблонURL.Комментарий);
		ОписаниеШаблонаURL.Вставить("template", ШаблонURL.Шаблон);

		Методы = ШаблонURL.Методы;
		ОписаниеМетодов = ОписаниеМетодовСервиса(Методы);
		
		ОписаниеШаблонаURL.Вставить("methods", ОписаниеМетодов);
		ОписаниеШаблоновURL.Добавить(ОписаниеШаблонаURL);
		
	КонецЦикла;
	
	Возврат ОписаниеШаблоновURL;

КонецФункции

Функция ОписаниеМетодовСервиса(Знач Методы)
	
	Перем Метод;
	Перем ОписаниеМетода;
	Перем ОписаниеМетодов;
	
	ОписаниеМетодов = Новый Массив;
	Для каждого Метод Из Методы Цикл
		ОписаниеМетода = Новый Структура;
		ОписаниеМетода.Вставить("name", Метод.Имя);
		ОписаниеМетода.Вставить("desc", Метод.Комментарий);
		ОписаниеМетода.Вставить("method", Строка(Метод.HTTPМетод));
		ОписаниеМетодов.Добавить(ОписаниеМетода);
	КонецЦикла;
	
	Возврат ОписаниеМетодов;

КонецФункции

Функция СтруктураВJSON(Знач Данные)
	
	Перем ЗаписьJSON;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

#КонецОбласти